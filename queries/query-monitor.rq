PREFIX ceo: <https://linkeddata.cultureelerfgoed.nl/def/ceo#>

SELECT
  ?item
  (COALESCE(?gisteren, 0) AS ?aantalGisteren)
  (COALESCE(?eergisteren, 0) AS ?aantalEergisteren)
  (COALESCE(?gisteren, 0) - COALESCE(?eergisteren, 0) AS ?verschil)
WHERE {
  {
    SELECT ?item (SUM(?c) AS ?gisteren)
    WHERE {
      GRAPH <{{GRAPH_GISTEREN}}> {
        ?item ceo:aantalInstanties ?c .
      }
    }
    GROUP BY ?item
  }
  OPTIONAL {
    SELECT ?item (SUM(?c) AS ?eergisteren)
    WHERE {
      GRAPH <{{GRAPH_EERGISTEREN}}> {
        ?item ceo:aantalInstanties ?c .
      }
    }
    GROUP BY ?item
  }
}
ORDER BY DESC(?verschil) ?item
